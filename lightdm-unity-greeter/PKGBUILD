# Maintainer: Hayate Nakamura <is01.njb at gmail dot com>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Taken from M0Rf30 morf3089 at gmail dot com

# vercheck-pkgbuild: auto
# vercheck-ubuntu: name=${pkgname#*-}, repo=yakkety, native
# vercheck-launchpad: name=${pkgname#*-}

pkgname=lightdm-unity-greeter
pkgver=22.04.1
_ubuntu_rel=0ubuntu2
pkgrel=1
pkgdesc="The greeter (login screen) application for Unity. It is implemented as a LightDM greeter."
arch=(i686 x86_64)
url="https://launchpad.net/unity-greeter"
license=(GPL3 LGPL3)
groups=(unity)
depends=(cantarell-fonts ido libindicator-gtk3 lightdm-ubuntu
         libcanberra libxext)
makedepends=(gnome-common unity-settings-daemon imagemagick
             intltool librsvg vala)
optdepends=("ubuntu-themes: Ubuntu's Ambiance and Radiance themes"
            "ubuntu-wallpapers: Ubuntu's default wallpapers"
            "ttf-ubuntu-font-family: Ubuntu's default font")
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname#*-}_${pkgver}-${_ubuntu_rel}.tar.xz"
        univalent-white.svg
        unity-greeter-indicators-start
        10-unity.defaults
        50-unity-greeter.rules
        logo.png)
sha512sums=('SKIP'
            'b0abf45e4cb4767742fe685b6ea24c036ade89f662cfe011f233a166274d1996827b5bb2fd3de9ea39800b739d6e9df7cf25eeab1972c14f0f706015e649e16d'
            'e43c177d0255af961bbf9198868e32a1a762bb70e117f80c3c2ce4b54d23f4955cc1c4c32b68751b021116f8e6d26133b24845c03ae459c2209e8313e28a0bc6'
            'ee5d1f17dddd99ba55bbaca8aff5e8487c9b9f7e1eef570d2adb3d2519e19ef437b160414468ef85a8c1b14af1eee23c714e2086291701edfbab2f43064e2cd7'
            '5d0f1b8221dfe02670df3ba88011dc0ce744bfd4205a9900dd8096de222358a740a384a47f13ed4e5b94e24d4a8ff5639117464c61a689dd7bd9025a0900f529'
            '90b3b2c6ef394b7406a1c50405bc2869b05aff7f98c6f4b33fd226f6da6b9f854e5fefd18376e7aefdd5d5005d03d96e8a02277e2da25c60cea35ad1f3c6e6aa')

prepare() {
    cd "${pkgname#*-}-${pkgver}"
    #cd trunk

    # Apply Ubuntu patches
    for i in $(grep -v '#' debian/patches/series); do
        patch -p1 -i "debian/patches/${i}"
    done

    # Use Arch Linux logo (retain the same 245px height as the Ubuntu logo)
    rm data/logo.png
    # Stupid segfaulting imagemagick...
    if ! convert -background none -resize 245 \
         ../univalent-white.svg \
         data/logo.png; then
        cp ../logo.png data/logo.png
    fi
}

build() {
    cd "${pkgname#*-}-${pkgver}"
    #cd trunk

    autoreconf -vfi
    intltoolize -f

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --sbindir=/usr/bin \
        --libexecdir=/usr/lib/lightdm

    make
}

package() {
    cd "unity-greeter-${pkgver}"
    #cd trunk

    make DESTDIR="${pkgdir}" install

    # Install PolicyKit file for allowing the lightdm user to use NetworkManager
    # Note: PolicyKit no longer reads pkla files after version 0.107, so Ubuntu's
    # policy file won't work.
    install -dm750 "${pkgdir}/usr/share/polkit-1/rules.d/"
    install -m644 "${srcdir}/50-unity-greeter.rules" \
        "${pkgdir}/usr/share/polkit-1/rules.d/"
    #install -dm700 "${pkgdir}/var/lib/polkit-1/"
    #install -dm755 "${pkgdir}/var/lib/polkit-1/localauthority/10-vendor.d/"
    #install -m644 "${srcdir}/debian/unity-greeter.pkla" \
    #    "${pkgdir}/var/lib/polkit-1/localauthority/10-vendor.d/"

    # Install default GSettings/dconf settings for the guest account (requires
    # lightdm-ubuntu).
    install -dm755 "${pkgdir}/etc/guest-session/gsettings/"
    install -m644 "${srcdir}/10-unity.defaults" \
        "${pkgdir}/etc/guest-session/gsettings/"

    # Install LightDM configuration file to set the Unity greeter as the default
    install -dm755 "${pkgdir}/usr/share/lightdm/lightdm.conf.d/"
    install -m644 debian/50-unity-greeter.conf \
                  "${pkgdir}/usr/share/lightdm/lightdm.conf.d/"

    # Install unity-gentoo's script for launcher the indicator services
    install -m755 "${srcdir}/unity-greeter-indicators-start" "${pkgdir}/usr/bin/"

    # Use language packs
    rm -r "${pkgdir}/usr/share/locale/"
}
